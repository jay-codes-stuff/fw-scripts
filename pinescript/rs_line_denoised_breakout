//@version=5
//(c) Jay
indicator("RS Line + Denoised bbreakout signals", overlay=false)

// Input parameters
comparisonSymbol = input.symbol("SPX", title="Comparison Symbol")
signalType = input.source(close,"Signal source")
lookback = input.int(1, title="Lookback Length")
slowLength = input.int(21, title="RS EMA length")
slopeNoiseThreshold = input.float(0.01,"Slope Noise threshold")

showBreakoutSignals = input.bool(true, title="Show Breakout Signals")
breakoutDenoise = input.float(0.01,title="Breakout denoise factor")
breakoutRsLookback = input.int(9,"Breakout RS Lookback")

//styles
rsLineUpColor=input.color(color.aqua,"Ascending RS Line")
rsLineDownColor=input.color(color.purple,"Descending RS Line")
shadingAboveColor=input.color(color.new(color.aqua,60))
shadingBelowColor=input.color(color.new(color.purple,60))


// Data retrieval and calculations
data2 = request.security(comparisonSymbol, timeframe.period, signalType) 
data = signalType
fastMA = ta.ema(data, lookback)
fastMAComparison = ta.ema(data2, lookback)
rs = fastMA / fastMAComparison

avg = ta.ema(rs, slowLength)
diff = rs - avg
lineColorSignal=rs>ta.ema(rs,9)
maUpwardSlope=avg>avg[1]*(1+slopeNoiseThreshold)

// Plotting
rsLine=plot(series=rs,color=lineColorSignal?rsLineUpColor:rsLineDownColor,linewidth = 2)
avgLine=plot(series=avg,color=color.orange)
fill(rsLine,avgLine,color=diff<0?shadingBelowColor:shadingAboveColor)


boRS=ta.ema(data,breakoutRsLookback)/ta.ema(data2,breakoutRsLookback)

// Breakout signals
upSignal = boRS/avg>(1+breakoutDenoise) and boRS[1]/avg[1]<(1+breakoutDenoise) ? 0 : na 
downSignal = boRS/avg<1/(1+breakoutDenoise) and boRS[1]/avg[1]>1/(1+breakoutDenoise) ? 0 : na 


plotshape(upSignal, style=shape.triangleup, location=location.absolute, color=color.green, size=size.tiny, display=showBreakoutSignals ? display.all : display.none)

plotshape(downSignal, style=shape.triangledown, location=location.absolute, color=color.red, size=size.tiny, display=showBreakoutSignals ? display.all : display.none)
